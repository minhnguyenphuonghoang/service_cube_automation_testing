*** Settings ***
Resource          Variables.txt

*** Keywords ***
Input First Name
    [Arguments]    ${first_name}
    Wait Until Page Contains Element    ${MyAccount_ChangeNamePopup_FirstNameTextField}    ${gl_timeout}    First name text field on Change Name popup - My Account couldn't be found
    Input Text    ${MyAccount_ChangeNamePopup_FirstNameTextField}    ${first_name}

Input Last Name
    [Arguments]    ${last_name}
    Wait Until Page Contains Element    ${MyAccount_ChangeNamePopup_LastNameTextField}    ${gl_timeout}    Last name text field on Change Name popup - My Account couldn't be found
    Input Text    ${MyAccount_ChangeNamePopup_LastNameTextField}    ${last_name}

Click Cancel
    Wait Until Page Contains Element    ${MyAccount_ChangeNamePopup_CancelButton}    ${gl_timeout}    CANCEL button of Change Name popup - My Account screen couldn't be found
    Click Element    ${MyAccount_ChangeNamePopup_CancelButton}
    Current screen is My Account screen

Click Save
    Wait Until Page Contains Element    ${MyAccount_ChangeNamePopup_SaveButton}    ${gl_timeout}    SAVE button of Change Name popup - My Account screen couldn't be found
    Click Element    ${MyAccount_ChangeNamePopup_SaveButton}

Change Name popup is disappeared
    Wait Until Element Is Not Visible    ${MyAccount_ChangeNamePopup_CancelButton}    ${gl_timeout}    Change Name popup should be disappeared but it is not
    Wait Until Element Is Not Visible    ${MyAccount_ChangeNamePopup_SaveButton}    ${gl_timeout}    Change Name popup should be disappeared but it is not

Change Name
    [Arguments]    ${first_name}    ${last_name}
    Input First Name    ${first_name}
    Input Last Name    ${last_name}
    Click Save

Save button is disabled
    Wait Until Keyword Succeeds    ${gl_timeout}    1    Element Should Be Disabled    ${MyAccount_ChangeNamePopup_SaveButton}

Save button is enabled
    Wait Until Keyword Succeeds    ${gl_timeout}    1    Element Should Be Enabled    ${MyAccount_ChangeNamePopup_SaveButton}

Clear First Name
    Wait Until Page Contains Element    ${MyAccount_ChangeNamePopup_FirstNameTextField}    ${gl_timeout}    First name text field on Change Name popup - My Account couldn't be found
    ${first_name}=    Get Value    ${MyAccount_ChangeNamePopup_FirstNameTextField}
    ${length}=    Get Length    ${first_name}
    :FOR    ${index}    IN RANGE    ${length}
    \    Selenium2Library.Press Key    ${MyAccount_ChangeNamePopup_FirstNameTextField}    \\8

Clear Last Name
    Wait Until Page Contains Element    ${MyAccount_ChangeNamePopup_LastNameTextField}    ${gl_timeout}    Last name text field on Change Name popup - My Account couldn't be found
    ${last_name}=    Get Value    ${MyAccount_ChangeNamePopup_LastNameTextField}
    ${length}=    Get Length    ${last_name}
    :FOR    ${index}    IN RANGE    ${length}
    \    Selenium2Library.Press Key    ${MyAccount_ChangeNamePopup_LastNameTextField}    \\8
