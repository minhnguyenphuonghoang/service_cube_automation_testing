*** Settings ***
Resource          Variables.txt
Variables         ../GuiObjects/ResetPassword.py

*** Keywords ***
Current screen is Reset Password screen
    Wait Until Page Contains    Reset your password    ${gl_timeout}
    Wait Until Page Contains Element    ${ResetPassword_CloseButton}    ${gl_timeout}    Close button couldn't be found on Reset Password screen
    Wait Until Page Contains Element    ${ResetPassword_EmailAddressTextField}    ${gl_timeout}    Email Address text field couldn't be found on Reset Password screen
    Wait Until Page Contains Element    ${ResetPassword_ContinueButton}    ${gl_timeout}    Continue button couldn't be found on Reset Password screen

Input Email Address_Reset Password
    [Arguments]    ${email_address}
    Wait Until Page Contains Element    ${ResetPassword_EmailAddressTextField}    ${gl_timeout}    Email Address text field on Reset Password screen couldn't be found
    Input Text    ${ResetPassword_EmailAddressTextField}    ${email_address}

Click Continue button
    Wait Until Page Contains Element    ${ResetPassword_ContinueButton}    ${gl_timeout}    CONTINUE button on Reset Password screen couldn't be found
    Click Element    ${ResetPassword_ContinueButton}

Click close icon
    Wait Until Page Contains Element    ${ResetPassword_CloseButton}    ${gl_timeout}    Close icon on Reset Password screen couldn't be found
    Click Element    ${ResetPassword_CloseButton}

Check Email Sent screen
    [Arguments]    ${email_address}
    Wait Until Page Contains Element    ${ResetPassword_EmailSentPopup_LeterIcon}    ${gl_timeout}    Mail icon on Reset Password - Email Sent screen couldn't be found
    Wait Until Page Contains Element    ${ResetPassword_EmailSentPopup_Title}    ${gl_timeout}    Title on Reset Password - Email Sent screen couldn't be found
    Wait Until Page Contains Element    ${ResetPassword_EmailSentPopup_Description}    ${gl_timeout}    Description on Reset Password - Email Sent screen couldn't be found
    Element Text Should Be    ${ResetPassword_EmailSentPopup_Title}    Check your inbox.
    ${description}=    Replace String    We've sent a link to reset your password to <email_address>.    <email_address>    ${email_address}
    Element Text Should Be    ${ResetPassword_EmailSentPopup_Description}    ${description}

Input Password_Reset Password_Set Password
    [Arguments]    ${password}
    Wait Until Page Contains Element    ${ResetPassword_SetPassword_PasswordTextField}    ${gl_timeout}    Password text field on Reset Password - Set Password screen couldn't be found
    Input Text    ${ResetPassword_SetPassword_PasswordTextField}    ${password}

Input Confirm New Password
    [Arguments]    ${password}
    Wait Until Page Contains Element    ${ResetPassword_SetPassword_ConfirmPasswordTextField}    ${gl_timeout}    Confirm Password text field on Reset Password - Set Password screen couldn't be found
    Input Text    ${ResetPassword_SetPassword_ConfirmPasswordTextField}    ${password}

Click Continue button_Reset Password_Set Password
    Wait Until Page Contains Element    ${ResetPassword_SetPassword_ContinueButton}    ${gl_timeout}    CONTINUE button on Reset Password - Set Password screen couldn't be found
    Click Element    ${ResetPassword_SetPassword_ContinueButton}

Set Password
    [Arguments]    ${password}    ${confirm_password}
    Input Password_Reset Password_Set Password    ${password}
    Input Confirm New Password    ${confirm_password}
    Click Continue button_Reset Password_Set Password

Set Password successful
    [Arguments]    ${password}    ${confirm_password}
    Set Password    ${password}    ${confirm_password}
    Wait Until Page Does Not Contain Element    ${password}    ${gl_timeout}    Reset Password - Set Password popup still exist after clicked on CONTINUE button
    Current screen is Home Page screen

Set Password unsuccessful
    [Arguments]    ${password}    ${confirm_password}    ${error_message}    ${error_type}
    Set Password    ${password}    ${confirm_password}

Click Service Cube Logo
    Wait Until Page Contains Element    ${ResetPassword_ServiceCubeLogo}    ${gl_timeout}    Service Cube Logo couldn't be found on Reset Password screen
    Click Element    ${ResetPassword_ServiceCubeLogo}

Current screen is Choose your password - Reset Password screen
    Wait Until Page Contains    Choose your new password    ${gl_timeout}
    Wait Until Page Contains Element    ${ResetPassword_SetPassword_PasswordTextField}    ${gl_timeout}    Password text field on Choose your password - Reset Password screen couldn't be found
    Wait Until Page Contains Element    ${ResetPassword_SetPassword_ConfirmPasswordTextField}    ${gl_timeout}    Confirm password text field on Choose your password - Reset Password screen couldn't be found
    Wait Until Page Contains Element    ${ResetPassword_SetPassword_ContinueButton}    ${gl_timeout}    Continue button on Choose your password - Reset Password screen couldn't be found

Continue button is disabled
    Element Should Be Disabled    ${ResetPassword_ContinueButton}

Verify error message on reset password screen
    [Arguments]    ${error_message}
    Wait Until Page Contains Element    ${ResetPassword_ErrorMessage}    ${gl_timeout}    Error message on Reset Password screen couldn't be found
    Wait Until Keyword Succeeds    60s    5s    Element Text Should Be    ${ResetPassword_ErrorMessage}    ${error_message}    Error message on Reset Password screen is incorrect!
